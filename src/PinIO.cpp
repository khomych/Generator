
#include <Arduino.h>

///////////////////////////////////////////////////////////////////////////////
//                                pinModeFast
///////////////////////////////////////////////////////////////////////////////
void pinModeFast(uint8_t pin, uint8_t mode) {
  switch (mode) {
    case INPUT:
      if (pin < 8) {
        bitClear(DDRD, pin);    
        bitClear(PORTD, pin);
      } else if (pin < 14) {
        bitClear(DDRB, (pin - 8));
        bitClear(PORTB, (pin - 8));
      } else if (pin < 20) {
        bitClear(DDRC, (pin - 14));   // Mode: INPUT
        bitClear(PORTC, (pin - 8));  // State: LOW
      }
      return;
    case OUTPUT:
      if (pin < 8) {
        bitSet(DDRD, pin);
        bitClear(PORTD, pin);
      } else if (pin < 14) {
        bitSet(DDRB, (pin - 8));
        bitClear(PORTB, (pin - 8));
      } else if (pin < 20) {
        bitSet(DDRC, (pin - 14));  // Mode: OUTPUT
        bitClear(PORTC, (pin - 8));  // State: LOW
      }
      return;
    case INPUT_PULLUP:
      if (pin < 8) {
        bitClear(DDRD, pin);
        bitSet(PORTD, pin);
      } else if (pin < 14) {
        bitClear(DDRB, (pin - 8));
        bitSet(PORTB, (pin - 8));
      } else if (pin < 20) {
        bitClear(DDRC, (pin - 14));  // Mode: OUTPUT
        bitSet(PORTC, (pin - 14));  // State: HIGH
      }
      return;
  }
}

///////////////////////////////////////////////////////////////////////////////
//                          digitalWriteFast
///////////////////////////////////////////////////////////////////////////////
void digitalWriteFast(uint8_t pin, bool x) {
  switch (pin) {            
    case 3: bitClear(TCCR2A, COM2B1);
      break;
    case 5: bitClear(TCCR0A, COM0B1);
      break;
    case 6: bitClear(TCCR0A, COM0A1);
      break;
    case 9: bitClear(TCCR1A, COM1A1);
      break;
    case 10: bitClear(TCCR1A, COM1B1);
      break;
    case 11: bitClear(TCCR2A, COM2A1);   // PWM disable 
      break;
  }
  if (pin < 8) {
    bitWrite(PORTD, pin, x);
  } else if (pin < 14) {
    bitWrite(PORTB, (pin - 8), x);
  } else if (pin < 20) {
    bitWrite(PORTC, (pin - 14), x);    // Set pin to HIGH / LOW 
  }
}

///////////////////////////////////////////////////////////////////////////////
//                              digitalReadFast
///////////////////////////////////////////////////////////////////////////////
bool digitalReadFast(uint8_t pin) {
  if (pin < 8) {
    return (bool)(bitRead(PIND, pin));
  } else if (pin < 14) {
    return (bool)(bitRead(PINB, pin - 8));
  } else if (pin < 20) {
    return (bool)(bitRead(PINC, pin - 14));    // Return pin state
  }
  return 0;
}

///////////////////////////////////////////////////////////////////////////////
//                              digitalToggleFast
///////////////////////////////////////////////////////////////////////////////
// быстро инвертирует состояние пина
void digitalToggleFast(uint8_t pin) {
  if (pin < 8) {
    bitSet(PIND, pin);
  } else if (pin < 14) {
    bitSet(PINB, (pin - 8));
  } else if (pin < 20) {
    bitSet(PINC, (pin - 14));    // Toggle pin state (for 'tone()')
  }
}